// Top-level build file where you can add configuration options common to all sub-projects/modules.

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:1.2.3'

        // NOTE: Do not place your application dependencies here; they belong
        // in the individual module build.gradle files
    }
}

allprojects {
    repositories {
        jcenter()
    }
}

/* Configure Unity app path using the parameter `unityInstallationPath` */
def DEFAULT_UNITY_PATH = "/Applications/Unity"
def UNITY_CLASSES_PATH = "/PlaybackEngines/AndroidPlayer/Variations/mono/Release/Classes/classes.jar"
def unityClassesPath;
if(hasProperty('unityInstallationPath') && 
    unityInstallationPath?.trim()) {
    unityClassesPath = unityInstallationPath + UNITY_CLASSES_PATH
} else {
    unityClassesPath = DEFAULT_UNITY_PATH + UNITY_CLASSES_PATH
}

println "Using Unity installation in : " + unityClassesPath

/* Injects configuration in all children projects */
subprojects{
    apply plugin: 'com.android.library'

    /* Configure plugins compilation */
    project.android {
        compileSdkVersion 24
        buildToolsVersion "24.0.0"


        defaultConfig {
            minSdkVersion 14
            targetSdkVersion 24
        }
    }

    /* Add a provided dependency with Unity java classes */
    project.dependencies{
        provided files(unityClassesPath)
    }

    /*
     * Copy release package to the Assets folder
     */
    task copyPluginPackage(type: Copy, group: 'socialpoint') {
        from('build/outputs/aar')
        into('../../../../Assets/Sparta/Binaries/Plugins/Android')
        include(project.name + '-release.aar')
        rename (project.name + '-release.aar', project.name + '.aar')
    }

    /*
     * Generates the .aar package and copy it to the Unity Project's Assets folder
     */
    task generateUnityPlugin
    generateUnityPlugin.dependsOn 'assembleRelease', 'copyPluginPackage'
    generateUnityPlugin.group 'socialpoint'
}
